
#Использовать fs

Перем КешМодулей;
Перем КаталогПроекта;
Перем КаталогИсходныхФайлов;
Перем ОбработчикиФормата;
Перем КлючХеширования;
Перем Лог;

#Область ПрограммныйИнтерфейс

Процедура УстановитьКлючХеширования(ПараметрКлючХеширования) Экспорт
	КлючХеширования = ПараметрКлючХеширования;
КонецПроцедуры

Функция НайтиПрограммныйМодуль(ВидПрограммногоМодуля, ИДПрограммногоМодуля) Экспорт

	Обработчик = ОбработчикиФормата.Получить(ВидПрограммногоМодуля);
	Если Обработчик = Неопределено Тогда
		ТестСообщения = СтрШаблон("Не найден обработчик для вида программного модуля ""%1""", ВидПрограммногоМодуля);
		Лог.Ошибка(ТестСообщения);
		Возврат Неопределено;
	КонецЕсли;

	КлючКеша = СтрШаблон("%1+%2", ИДПрограммногоМодуля, ВидПрограммногоМодуля);
	ЗначениеКеша = КешМодулей.Получить(КлючКеша);
	Если ТипЗнч(ЗначениеКеша) = Тип("Структура") Тогда
		Возврат ЗначениеКеша;
	КонецЕсли;

	ПутьКИсходнымФайлам = ОбъединитьПути(КаталогПроекта, КаталогИсходныхФайлов);
	КаталогИсходников = Новый Файл(ПутьКИсходнымФайлам);
	ПутьПрограммногоМодуля = Обработчик.Выполнить(КаталогИсходников.ПолноеИмя, ИДПрограммногоМодуля);
	Если ТипЗнч(ПутьПрограммногоМодуля) = Тип("Строка") Тогда
		
		ПутьВПроекте = ФС.ОтносительныйПуть(КаталогПроекта, ПутьПрограммногоМодуля);
		Если Не ФС.ФайлСуществует(ПутьПрограммногоМодуля) Тогда
			ТестСообщения = СтрШаблон("Программный модуль не существует ""%1""", ПутьВПроекте);	
			Лог.Ошибка(ТестСообщения);
			Возврат Неопределено;
		КонецЕсли;
		
		ЗначениеКеша = ПрограммныйМодуль(ПутьВПроекте);
		КешМодулей.Вставить(КлючКеша, ЗначениеКеша);
		Возврат ЗначениеКеша;
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта(ПараметрКаталогПроекта, ПараметрКаталогИсходныхФайлов, ФорматИсходныхФайлов) Экспорт

	Если ФорматИсходныхФайлов = ФорматыИсходныхФайлов.EDT Тогда
		ОбработчикиФормата = ОбработчикиФорматов.ОбработчикиФормата(ОбработчикиФорматаEDT);
	Иначе
		ВызватьИсключение СтрШаблон("Неизвестный формат исходных файлов: ""%1""", ФорматИсходныхФайлов);
	КонецЕсли;

	КаталогПроекта = ПараметрКаталогПроекта;
	КаталогИсходныхФайлов = ПараметрКаталогИсходныхФайлов;

	КешМодулей = Новый Соответствие;	

	Лог = ПараметрыПриложения.Лог();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрограммныйМодуль(ПутьПрограммногоМодуля)

	ПрограммныйМодуль = Новый Структура;
	ПрограммныйМодуль.Вставить("Путь", ПутьПрограммногоМодуля);
	ПрограммныйМодуль.Вставить("Идентификатор", XMLСтрока(Новый УникальныйИдентификатор()));
	Возврат ПрограммныйМодуль; 

КонецФункции

#КонецОбласти
